plugins {
	id 'dev.architectury.loom' version '1.9-SNAPSHOT' apply false
	id 'architectury-plugin' version '3.4-SNAPSHOT'
	id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
	id "io.github.pacifistmc.forgix" version "1.2.9"
}

architectury {
	minecraft = project.minecraft
}

allprojects {
	group = project.maven_group
	version = project.mod_version
}

subprojects {
	apply plugin: 'dev.architectury.loom'
	apply plugin: 'architectury-plugin'
	apply plugin: 'maven-publish'

	base {
		archivesName = "$project.archive_name-$project.name"
	}

	repositories {
		maven { url = "https://jitpack.io" }
		maven { url = "https://maven.terraformersmc.com/releases/" }
		mavenCentral()
		mavenLocal()
		exclusiveContent {
			forRepository {
				maven {
					name = "Modrinth"
					url = "https://api.modrinth.com/maven"
				}
			}
			filter {
				includeGroup "maven.modrinth"
			}
		}
		flatDir {
			dirs("../libs")
			dirs("libs")
		}
		maven { url = "https://maven.quiltmc.org/repository/release/" }
	}

	loom {
		mods {
			"perspective" {
				sourceSet("main")
			}
		}
	}

	dependencies {
		minecraft "net.minecraft:minecraft:$project.minecraft"
		mappings("net.fabricmc:yarn:${project.mappings}:v2")
	}

	java {
		withSourcesJar()

		sourceCompatibility = JavaVersion.VERSION_21
		targetCompatibility = JavaVersion.VERSION_21
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.release = 21
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				artifactId = base.archivesName.get()
				from components.java
			}
		}

		repositories {
		}
	}
}

forgix {
	group = "${project.maven_group}.${project.archive_name}"
	mergedJarName = "${project.archive_name}-${project.version}+${project.minecraft} " + (project.isDirty == "true" ? "+dirty." + System.currentTimeSeconds() : "") + ".jar"
	outputDir = "build/libs/merged"
//	neoforge {
//		projectName = "neoforge"
//		jarLocation = "build/libs/${project.archives_name}-neoforge-${project.version}.jar"
//		additionalRelocate "com.mclegoman.perspective", "neoforge.com.mclegoman.perspective"
//	}
	quilt {
		projectName = "quilt"
		jarLocation = "build/libs/${project.archive_name}-quilt-${project.version}.jar"
		additionalRelocate "com.mclegoman.perspective", "quilt.com.mclegoman.perspective"
	}
	fabric {
		projectName = "fabric"
		jarLocation = "build/libs/${project.archive_name}-fabric-${project.version}.jar"
		additionalRelocate "com.mclegoman.perspective", "fabric.com.mclegoman.perspective"
	}
}